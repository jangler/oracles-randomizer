common:
  # allow skipping the capcom screen after one second by pressing start.
  03/skipCapcom: |
      push hl
      ld a,(cbb3)
      cp a,94
      jr nc,.noSkip
      call forceEnableIntroInputs
      .noSkip
      pop hl
      jp decHlRef16WithCap

  # use different seed capacity table, so that level zero satchel can still
  # hold 20 seeds.
  3f/seedCapacityTable: |
      db 20,20,50,99

seasons:
  03/4d6b/: call skipCapcom

  # remove hard-coded d5 boss key room beep - it beeps just fine based on
  # normal dungeon room properties.
  01/4a09/: nop; nop

  # make link actionable as soon as he drops into the world.
  05/4d98/: jp setLinkIdOverride

  # let link jump down the cliff outside d7, in case of winter sans shovel.
  # also let link jump down the snow cliff added in woods of winter. also call
  # devJump if applicable.
  05/cliffLookup: |
      call devJump
      ret c
      push af
      ld a,(wActiveGroup)
      or a
      jr nz,.noJumpPopAf
      ld a,(wActiveRoom)
      cp a,d0
      jr nz,.notD7Entrance
      pop af
      cp a,a8
      jr nz,.noJump
      ld a,08
      scf
      ret
      .notD7Entrance
      cp a,9d
      jr nz,.noJumpPopAf
      pop af
      cp a,99
      jr z,.snowJump
      cp a,9b
      jr nz,.noJump
      .snowJump
      ld a,10
      scf
      ret
      .noJumpPopAf
      pop af
      .noJump
      jp lookupCollisionTable
  05/5fe8/: call cliffLookup

  # replace a random item drop with gale seeds 1/4 of the time if the player is
  # out of gale seeds. just to be nice since warping out of one-ways is in
  # logic.
  06/dropExtraGalesOnEmpty: |
      ld a,TREASURE_GALE_SEEDS
      call checkTreasureObtained
      jr nc,.done
      ld l,b8
      or (hl)
      jr nz,.done
      call getRandomNumber
      cp a,40
      jr nc,.done
      ld c,08
      .done
      call getFreePartSlot
      ret
  06/47f5/: call dropExtraGalesOnEmpty

  # custom script command to use on d1 entrance screen: disable warp tiles
  # until bit of cfc0 is set. fixes a vanilla bug where dismounting an animal
  # on that screen allowed you to enter without the key.
  0b/d1EntranceScriptCmd: |
      pop hl
      push bc
      ld bc,9600
      call compareRoom
      pop bc
      ret nz
      ld a,01
      ld (wDisableWarpTiles),a
      xor a
      jp 432d
  # new script command address and id
  0b/406d/: dw d1EntranceScriptCmd
  0b/4dea/: db b2

  # move sleeping talon and his mushroom so they don't block the chest.
  0b/6080/: db 78,a8 # mushroom
  11/6d2b/: db 88,a8 # talon

  # make all seeds grow in all seasons.
  0d/68b5/: cp a

  # allow harvesting seeds from trees with either slingshot or satchel.
  10/checkCanHarvestSeeds: |
      call checkTreasureObtained
      ret c
      ld a,TREASURE_SLINGSHOT
      jp checkTreasureObtained
  10/4b1a/: call checkCanHarvestSeeds

  # move the hitbox for the bridge lever from holodrum plain to natzu to the
  # top-left corner of the screen, where it can't be hit, and replace the lever
  # tile as well. this prevents the bridge from blocking the waterway.
  11/6737/: db 00,00 # object
  21/6267/: db 04 # ricky tile
  23/5cb7/: db 04 # moosh tile

  # feather game: don't give fools ore, and don't return fools ore.
  14/4881/: db delay1frame,delay1frame,delay1frame
  # always give up feather at the end if the player doesn't have it.
  15/5dcf/: |
      ld a,TREASURE_FEATHER
      call checkTreasureObtained
      jr c,05
  # and make the feather appear without needing to be dug up.
  15/5335/: db 1a # treasure mode
  # allow transition away from the screen once you have feather (not once the
  # hole is dug).
  09/65a0/: |
      ld a,TREASURE_FEATHER
      call checkTreasureObtained
      nop
      nop
      ret nc

  # move d8 magnet ball one tile to the left, so you don't get stuck if
  # you go up the stairs without magnet glove.
  15/4f62/: db 38

  3f/460d/: ld hl,seedCapacityTable

ages:
  # vanilla bug: compass doesn't show D6 boss key chest.
  01/4eea/: db 34

  03/4d6b/: call skipCapcom

  # don't spawn maku path skeletons when entering from the right.
  # this prevents softlocking by running out of ammunition.
  02/despawnMakuPathSkeletons: |
      ld a,(wScreenTransitionDirection)
      cp a,03
      jr nz,.done
      push bc
      ld bc,0604
      call compareRoom
      pop bc
      jr nz,.done
      ld a,06
      jp 77f0
      .done
      ld a,(wActiveRoom)
      jp 77c9
  02/77c6/: jp despawnMakuPathSkeletons

  # make guy in front of d2 go away if you have bombs.
  09/5242/: |
      ld a,03
      call checkTreasureObtained
      jp c,interactionDelete
  # and center him on a tile so you can't get stuck in a currents loop.
  12/611c/: db 48

  # remove the entire crescent island storm / tokay theft sequence.
  09/5731/: jp 582a              # tokays stealing items
  09/5756/: jp 582a              # ^
  09/57a5/: inc a; inc a         # ^
  0a/4fe1/: jp interactionDelete # ^
  0a/623a/: or a; or a; or a     # tokay trader stolen items
  0a/62d7/: or a; or a; or a     # ^
  0b/52e3/: jp interactionDelete # storm
  12/638f/: db f3,57,41,ff       # item in chicken hut

  # never spawn hide and seek event in fairies' woods. apparently you're frozen
  # if you enter on an animal?
  0a/52bf/: jp interactionDelete

  # allow buying tokay trader's shield if you have scent seeds but not satchel.
  0a/629c/: jr nc,00

  # stop d6 boss key chest from setting past boss key flag.
  10/793c/: ret; nop; nop

  # first and second time portals (near maku tree) are always active.
  10/7d4e/: jr nz,00 # present -> past
  10/7d57/: jr c,00  # past -> present

  # allow harvesting seeds from trees with either shooter or satchel.
  11/checkCanHarvestSeeds: |
      call checkTreasureObtained
      ret c
      ld a,TREASURE_SHOOTER
      jp checkTreasureObtained
  11/4aba/: call checkCanHarvestSeeds

  # remove the usual deku soldier event (taken to palace etc).
  12/58f5/: ret
  # cut off end of soldier's text so it makes sense when giving item.
  23/6656/: db 00
  # position the "you may go now" text correctly on screen.
  23/65d8/: db 00

  # move worker on ricky nuun cave screen off the "roof", and seal of the roof
  # with a railing, preventing a currents softlock.
  12/5a9e/: db 68,48
  23/718e/: db 72,50,50,73

  # don't equip sword in shooting gallery if link doesn't have it. it wouldn't
  # work anyway.
  15/setShootingGalleryEquips: |
      ld a,TREASURE_SWORD
      call checkTreasureObtained
      ld a,00
      ldi (hl),a
      ret nc
      dec hl
      ld a,TREASURE_SWORD
      ldi (hl),a
      ret
  15/50ae/: call setShootingGalleryEquips

  # prevent stairs disappearing in event where maku tree is attacked by
  # moblins, preventing softlock if player gets there with seed satchel and no
  # sword or something stupid.
  15/6bf3/: db jumpalways; dw c315

  # prevent bridge-building foreman from setting flag 22 so that animal/flute
  # event doesn't happen in fairies' woods.
  15/75bf/: db setglobalflag,a2 # bit 7 = unset flag

  3f/4607/: ld hl,seedCapacityTable

  # static pegasus seed item drop in moosh nuun
  12/nuunMooshConditionalPegDropObjects: |
      db f2
      db 9a,d4,68,48     # vanilla worker object
      db f0,04           # conditional moosh
      db fa              # obj_itemDrop
      db 00,07,58        # 07 = peg seeds, 58 = coordinates
      db fe
  # 12/5aa0/: db 9a,d4,68,48    # these 4 bytes are replaced with:
  12/5aa0/: |
      db f3; dw nuunMooshConditionalPegDropObjects
      db f2