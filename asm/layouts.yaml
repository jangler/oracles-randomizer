# static and dynamic changes to tile layouts for rooms.

common:
  # look up tiles in custom replacement table after loading a room. the format
  # is (group, room, bitmask, YX, tile ID), with ff ending the table. if the
  # bitmask AND the current room flags is nonzero, the replacement is not made.
  04/applyExtraTileSubstitutions: |
      push bc
      push de
      call getThisRoomFlags
      ld e,a
      ld hl,tileSubTable
      ld a,(wActiveGroup)
      ld b,a
      ld a,(wActiveRoom)
      ld c,a
      .loop
      ldi a,(hl)
      cp a,ff
      jr z,.done
      cp b
      jr nz,.groupMismatch
      ldi a,(hl)
      cp c
      jr nz,.roomMismatch
      ldi a,(hl)
      and e
      jr nz,.flagMismatch
      push de
      ld d,cf
      ldi a,(hl)
      ld e,a
      ldi a,(hl)
      ld (de),a
      pop de
      jr .loop
      .groupMismatch
      inc hl
      .roomMismatch
      inc hl
      .flagMismatch
      inc hl
      inc hl
      jr .loop
      .done
      pop de
      pop bc
      call applyAllTileSubstitutions
      ret

# bank 21 = spring, 22 = summer, 23 = autumn, 24 = winter.
seasons:
  # override room layout with bank 02's winterLayout when appropriate.
  00/loadWinterLayout: |
      push de
      ld a,(wRoomStateModifier)
      cp a,03 # winter
      jr nz,.done
      push bc
      ld bc,9d00
      call compareRoom
      pop bc
      jr nz,.done
      ld a,02 # bank
      ld (ff00+8c),a
      ld hl,winterLayout
      .done
      ld a,(ff00+8c)
      jp 39e2 # can't just ret, idr why
  00/39df/: jp loadWinterLayout

  # custom room layout for the problematic woods of winter screen in winter.
  # the code here is one 8-tile compression block per line.
  02/winterLayout: |
      db 55,80,81,81,81,81
      db 7c,16,80,82,17
      db f0,1b,c4,c4,70,72
      db 00,01,0d,17,c4,80,81,70,71
      db 60,04,70,71,1a,1b,1c,f7
      db 05,80,81,81,70,71,9e,9e
      db 1c,16,04,15,17,80,81
      db 30,1b,99,9b,d9,1a,01,19
      db 00,70,71,15,16,17,f7,7a,8c
      db 11,18,19,80,81,01,19,70

  # data for applyAllTileSubstitutions: group,room,flags,yx,tile
  04/tileSubTable: |
      db 00,01,01,52,04 # permanently remove flower outside D6 when cut
      db 00,25,00,32,3a # add ledge down from temple remains lower portal
      db 00,25,00,33,cf # ^
      db 00,25,00,34,4b # ^
      db 00,5c,00,64,48 # extend moblin keep railing as chokepoint for warning
      db 00,5c,00,74,53 # cont.
      db 00,9a,00,14,12 # remove rock across pit blocking exit from D5
      db 00,8a,00,66,64 # ^ but add rock at bottom of cliff to block ricky
      db 00,9a,00,34,04 # remove bush next to rosa portal
      db 00,b0,00,21,13 # remove spool swamp pits to prevent winter softlock
      db 00,b0,00,51,13 # cont.
  04/d2AltEntranceTileSubs: |
      # these are enabled only if entrances are randomized
      db fe,8d,00,18,04 # remove left stairs
      db fe,8e,00,12,04 # remove right stairs
  04/endTileSubTable: |
      db ff

  # replaces some tiles in shallow spool swamp in spring, in addition to the
  # regular tile sub routine.
  04/applySeasonsTileSubstitutions: |
      push bc
      ld bc,9100
      call compareRoom
      pop bc
      jr nz,.done
      ld a,(wRoomStateModifier)
      and a
      jr nz,.done
      ld hl,cf64
      ld a,68
      .loop
      ld (hl),fa
      inc l
      cp l
      jr nc,.loop
      .done
      jp applyExtraTileSubstitutions
  00/3854/: call applySeasonsTileSubstitutions

  # unset a room flag when the flower outside D6 is broken. see tileSubTable.
  06/checkBreakD6Flower: |
      push af
      push bc
      ld bc,0100
      call compareRoom
      pop bc
      jr nz,.done
      ld a,(ff00+hBrokenTilePosition)
      cp a,52
      jr nz,.done
      push hl
      ld hl,c701
      res 0,(hl)
      pop hl
      .done
      pop af
      jp setTile
  06/4774/: call checkBreakD6Flower

  # change water tiles outside d4 from deep to shallow, to prevent softlock
  # from entering without flippers or default summer.
  21/54a9/: db fa,6b,6b,53,fa,3f,fa
  22/5197/: db fa,6b,6b,53,fa,3f,fa
  23/4f6c/: db fa,6b,6b,53,fa,3f,fa
  24/4cec/: db dc,00,fc,06,dc,dc,dc,dc

  # block the waterfalls from mt cucco to sunken city, so that there only needs
  # to be one warning interaction at the vines.
  21/5bd1/: db 40,40,40
  21/5c17/: db 40,40,40
  22/58a4/: db 40,40,40
  22/58ea/: db 40,40,40
  23/5645/: db 40,40,40
  23/568b/: db 40,40,40
  24/54fa/: db 40,40,40
  24/5540/: db 40,40,40

  # replace some currents in spool swamp in spring so that the player isn't
  # trapped by them.
  21/7ab1/: db d3,d3
  21/7ab6/: db d4,d4,d4
  21/7abe/: db d1

  # replace deep water on spring floodgate keyhole screen, and the screen below
  # it, with shallow water. applySeasonsTileSubstitutions also contributes to
  # this, since in-place changes can't keep deep water tiles on the edge of the
  # screen.
  21/6d9b/: db fa,fa,fa
  21/6da3/: db fa
  21/6db4/: db fa,fa,fa
  21/6dbd/: db fa
  21/71f1/: db fa,fa,fa
  21/71f9/: db fa,fa
  21/71fd/: db fa
  21/7208/: db fa
  21/7214/: db fa

  # replace deep water in eyeglass lake water with shallow water to prevent a
  # softlock from default winter + summer rod + no flippers.
  22/74e4/: db fa
  22/78ed/: db fa,fa,fa,fa

  # replace the stairs outside the portal in eyeglass lake summer with a
  # railing, since if the player jumps off they fall into lost woods.
  22/791b/: db 40,40,40
  # instead add a ledge to the left side of the platform, so that entering the
  # portal without feather and resetting the season to summer isn't a softlock.
  22/78fd/: db 37
  22/7905/: db 25
  22/7910/: db 47

  # remove snow pile outside d6 to prevent softlock in default winter if statue
  # is pushed to the left.
  24/45d2/: db 04

  # remove the snow piles in front of holly's house so that shovel isn't
  # required to leave.
  24/6474/: db 04
  # remove some snow outside d7 for the same reason - but off the right ledge,
  # not in front of the stairs.
  24/7910/: db 2b,a0,b9,2b

ages:
  00/38c0/: call applyExtraTileSubstitutions

  # data for applyAllTileSubstitutions: group,room,flags,yx,tile
  04/tileSubTable: |
      db 00,20,00,61,d7 # portal in talus peaks
      db 01,48,00,45,d7 # portal south of past maku tree
      db 00,37,02,43,d7 # portal in southeast ricky/moosh nuun
      db 00,39,00,63,f0 # open chest on intro screen
      db 00,39,20,63,f1 # closed chest on intro screen
      db 00,6b,00,42,3a # removed tree in yoll graveyard
      db 00,6b,02,42,ce # not removed tree in yoll graveyard
      db 00,83,00,43,a4 # rock outside D2
      db 03,0f,00,66,f9 # water in d6 past entrance
      db 01,13,00,61,d7 # portal in symmetry city past
      db 01,13,00,68,d7 # portal in symmetry city past
      db 00,25,00,37,d7 # portal in nuun highlands
      db 05,da,01,a4,b2 # tunnel to moblin keep
      db 05,da,01,a5,b2 # cont.
      db 05,da,01,a6,b2 # cont.
      db 00,24,02,49,63 # other side of symmetry city bridge
      db 00,24,02,59,63 # cont.
      db 00,24,02,69,63 # cont.
      db 00,24,02,79,73 # cont.
      db 01,2c,00,70,69 # ledge in rolling ridge east past
      db 01,2c,00,71,06 # cont.
      db 01,2c,00,72,67 # cont.
      db 00,a9,00,67,f2 # portal sign on crescent island
      db 01,a5,00,35,48 # ledge by library past
      db 01,a5,00,45,0b # cont.
      db 01,a5,00,55,6c # cont.
      db 00,83,00,44,d7 # portal outside D2 present
      db 01,48,02,31,cd # past maku road: remove dirt when exiting
      db ff

  # burning the first tree in yoll graveyard should set room flag 1 so that it
  # can be gone for good (see tileSubTable).
  06/removeYollTreeWhenBurned: |
      push af
      ld a,(ff00+8f)
      cp a,0c
      jr nz,.done
      push bc
      ld bc,6b00
      call compareRoom
      jr nz,.next
      ld hl,c76b
      set 1,(hl)
      .next
      pop bc
      .done
      pop af
      ld hl,wTotalSignsDestroyed
      ret
  06/47aa/: call removeYollTreeWhenBurned

  # set room flag for tunnel behind keep when defeating great moblin (see
  # tileSubTable).
  0f/openMoblinKeepTunnel: |
      ld hl,c709
      set 0,(hl)
      ld hl,cada
      ret
  0f/7f3e/: call openMoblinKeepTunnel

  # set room flag for other side of symmetry city bridge at end of building
  # cutscene (see tileSubTable).
  15/setBridgeFlag: |
      push hl
      xor a
      ld (wDisabledObjects),a
      ld a,25
      call setGlobalFlag
      ld hl,c724
      set 1,(hl)
      pop hl
      ret
  0c/7a6f/: db asm15; dw setBridgeFlag

  # put a "return bush" for long hook across from syrup's shop.
  23/7ea0/: db 27,c8
  23/7ead/: db 22

  # change bush type in moosh nuun (for static peg seed drop)
  26/4501/: db c8

  # change maku path hidden stairs tile to visible stair tile.
  # remove the object responsible for spawning the stairs.
  26/7042/: db 45
  12/6572/: |
      db f8,09,00,14
      db f3,68,40
      db ff

  # give "goron's hiding place" bombable wall a different tile, like other
  # bombable walls have.
  28/6ac0/: db 31

  # Maku Path: Prevent Softlock when exiting cave (see tileSubTable)
  25/5c9a/: db 3a
  26/4a9e/: db 3a
